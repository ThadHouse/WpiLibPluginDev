defineWpiLibProperties()

def implSetupModel = { project ->
    project.model {
        components {
            impl(NativeLibrarySpec) {
                if (project.isArm) {
                    targetPlatform 'arm'
                } else {
                    targetPlatform 'x86'
                    targetPlatform 'x64'
                }
                setupDefines(project, binaries)
                
                
                                binaries.all {
                tasks.withType(CppCompile) {
                    addUserLinks(linker, targetPlatform)
                    addWpilibLibraryLinks(it, linker, targetPlatform)
                    //addWpiLibLink(linker, targetPlatform)
                }
                }

                if (includeJava) {
                    project.setupJniIncludes(binaries)
                    binaries.all {
                        project.setupDef(linker, "${rootDir}/impl-jni.def")
                    }
                } else {
                    binaries.all {
                        project.setupDef(linker, "${rootDir}/impl.def")
                    }
                }

                sources {
                    cpp {
                        source {
                            srcDirs = ["${rootDir}/src"]
                            if (includeJava) {
                                srcDirs "${rootDir}/java/lib"
                            }
                            includes = ["**/*.cpp"]
                        }
                        exportedHeaders {
                            srcDirs = ["${rootDir}/include", "${rootDir}/libraries/include", wpilibInclude]
                            if (includeJava) {
                                project.jniHeadersImpl.outputs.files.each { file ->
                                    srcDirs file.getPath()
                                }
                            }
                            includes = ['**/*.h']
                        }
                        if (project.isArm) {
                            lib project: ':arm:driver', library: 'driver', linkage: 'static'
                        } else {
                            lib project: ':native:driver', library: 'driver', linkage: 'static'
                        }
                    }
                }
            }
        }
    }
}

def implZipTask = { pjt ->
    pjt.ext.implZip = pjt.tasks.create("${pjt.isArm ? 'arm' : 'native'}implZip", Zip) {
        description = 'Creates platform-specific zip of the desktop impl libraries.'
        group = 'WPILib'
        destinationDir = pjt.buildDir
        baseName = 'impl'
        classifier = "${pjt.buildPlatform}"
        duplicatesStrategy = 'exclude'

        from(file('include')) {
            into 'include'
        }

        if (!pjt.hasProperty('skipJava')) {
            pjt.jniHeadersImpl.outputs.each {
                from(it) {
                    into 'include'
                }
            }
        }

        pjt.model {
            binaries {
                withType(StaticLibraryBinarySpec) { binary ->
                    from(binary.staticLibraryFile) {
                        into 'lib'
                    }
                }
                withType(SharedLibraryBinarySpec) { binary ->
                    from(binary.sharedLibraryFile) {
                        into 'lib'
                    }
                }
            }
        }

        // Include the static library file and shared library object from driver project
        def hal = project(':arm:driver')
        hal.model{
            binaries{
                withType(StaticLibraryBinarySpec) { spec ->
                    spec.headerDirs.each {
                        from(it) {
                            into 'include'
                            // We don't want to include any of the .cpp files that are in some of the header directories
                            exclude '**/*.cpp'
                        }
                    }
                    from(spec.staticLibraryFile) {
                        into 'lib'
                    }
                }
                withType(SharedLibraryBinarySpec) { spec ->
                    from(spec.sharedLibraryFile) {
                        into 'lib'
                    }
                }
            }
        }

        from (halSharedLib) {
            into 'lib'
        }

        from (halStaticLib) {
            into 'lib'
        }

        from(ntcoreSharedLib) {
            into 'lib'
        }
        from(ntcoreStaticLib) {
            into 'lib'
        }
        from(wpiUtilStaticLib) {
            into 'lib'
        }

        rename ("libHALAthena_shared.so", "libHALAthena.so")
        rename ("libntcore_shared.so", "libntcore.so")
    }

    pjt.build.dependsOn pjt.implZip

    def releaseTasks = [pjt.implZip]

    if (includeJava) {
        releaseTasks.add(pjt.jar)
    }

    pjt.releaseSetup(releaseTasks)

    pjt.tasks.whenTaskAdded { task ->
        def name = task.name.toLowerCase()
        if (name.contains("implsharedlibrary") || name.contains("implstaticlibrary")) {
            pjt.implZip.dependsOn task
        }
    }
}

project(':arm:impl') {
  apply plugin: 'cpp'

  apply from: "${rootDir}/toolchains/arm.gradle"
  if (includeJava) {
    apply from: "${rootDir}/java/java.gradle"
  }

  implSetupModel(project)
  implZipTask(project)
}

task implSourceZip(type: Zip) {
    description = 'Creates a sources-zip of the impl source files'
    group = 'WPILib'
    destinationDir = project.buildDir
    baseName = 'impl'
    classifier = "sources"
    duplicatesStrategy = 'exclude'

    from('src') {
        into 'src'
    }

    from('include') {
        into 'include'
    }

    if (includeJava) {
        from('java/lib') {
            into 'src'
        }
        project(':arm:impl').jniHeadersImpl.outputs.each {
            from(it) {
                into 'include'
            }
        }
    }
}
